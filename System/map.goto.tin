#ALIAS {.mapRun}
{
  #NOP %1;
  #IF {"%1" == "stop"}
  {
    #UNVAR {map[run]};
    #UNTICKER {tWalk};
    #UNACTION {#END OF PATH.};
  };
  #ELSEIF {"%1" == "pause"}
  {
    #UNTICKER {tWalk};
  };
  #ELSE
  {
    #IF {"%1" != ""}
    {
      #VAR {map[run][dest]} {%1};
      #MAP FIND $map[run][dest];
    };
    #IF {"$map[run][dest]" != ""}
    {
      #ACTION {#END OF PATH.}
      {
        .mapRun {stop};
      };
      #TICKER {tWalk} {#PATH {walk};} {0.01};
    };
  };
};

#ALIAS {.goto}
{
  #NOP %1;
  #UNVAR {map[destination]};
  #UNVAR {map[rooms]};

  #IF {"%1" == ""}
  {
    #ECHO {You must enter something to search for!};
  };
  #ELSE
  {
    #FOREACH {roomnote;roomarea} {v}
    {
      #IF {"$map[rooms][+1]" == ""}
      {
        #MAP list {$v} {%i%*%0%*} {variable} {map[rooms]};
      };
    };

    #IF {"$map[rooms][+1]" != ""}
    {
      #IF {"$map[rooms][+2]" == ""}
      {
        #FOREACH {$map[rooms][]} {r}
        {
          #VAR {map[destination]} {$r};
        };
        .mapRun $map[destination];
      };

      #ELSE
      {
        #ECHO {Please select from the list (0 to cancel):};
        #CLASS {cMapGotoMacros} {open};
        #MACRO {0}
        {
          #CLASS {cMapGotoMacros} {kill};
          #ECHO {Cancelled};
        };
        #ALIAS {.GotoMacro}
        {
          #MACRO {%%1}
          {
            #NOP delay added because ticker wouldn't add from macro for some reason;
            #DELAY {0.1} {.mapRun %%2};
            #CLASS {cMapGotoMacros} {kill};
          };
        };

        #VAR {i} {0};

        #FOREACH {$map[rooms][]} {r}
        {
          #MATH {i} {$i + 1};
          #IF {$i < 27}
          {
            #FORMAT {map[temp][macroletter]} {%a} {$i+96};
            .GotoMacro {$map[temp][macroletter]} {$r};
            #VAR {map[temp][info]} {@fMapRoomInfo{$r}};
            #ECHO {$map[temp][macroletter] : $map[rooms][$r] $map[temp][info][roomname] ($map[temp][info][roomarea])};
          };
          #IF {$i == 27}
          {
            #ECHO {Further matches not shown.};
          };
        };
      };
    };
  };
};
