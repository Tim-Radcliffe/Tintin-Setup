#VAR {window} {};

#NOP {windowname} {windowline} {content};
#ALIAS {.WindowSetLine}
{
  #NOP %1;
  .WindowCreate {%1};
  #VAR {window[buffer][%1][%2]} {%3};
  #VAR {window[update][%1]} {1};
  .WindowNotify
};

#ALIAS {.WindowSet}
{
  #NOP %1;
  .WindowCreate {%1};
  #VAR {window[buffer][%1]} {%2};
  #VAR {window[update][%1]} {1};
  .WindowNotify
};

#ALIAS {.WindowAppend}
{
  #NOP %1;
  .WindowCreate {%1};
  #LIST {window[buffer][%1]} {add} {%2};
  #VAR {window[tail][%1]} {1};
  #VAR {window[update][%1]} {1};
  .WindowNotify
};

#ALIAS {.WindowCreate}
{
  #NOP %1;
  #IF {"$window[names][%1]" != "1"}
  {
    #VAR {window[names][%1]} {1};
    #LIST {window[index]} {CREATE} {$window[names][]};
  };  
};

#ALIAS {.WindowNotify}
{
    #NOP %1;
    #VAR {_WN} {};
    #FOREACH {$window[names][]} {w}
    {
        #IF {"$w" == "$window[index][1]"}
        {
            #VAR {_WN} {${_WN}>$w< };        
        };
        #ELSEIF {"$window[update][$w]" == "1"}
        {
            #VAR {_WN} {${_WN}*$w* };        
        };
        #ELSE
        {
            #VAR {_WN} {${_WN} $w  };
        };
    };
    event_raise {e_notify} {window} {<274>$_WN} {2};    
    #UNVAR {_WN};
};

#ALIAS {.WindowSwap}
{
  #NOP %1;
  #IF {"$window[index][1]" == "" } {#RETURN;};
  #LIST {window[index]} {add} {$window[index][1]};
  #LIST {window[index]} {delete} {1};
  .WindowRefresh {$window[index][1]};
  .WindowNotify;
};

#ALIAS {.WindowRefreshLine}
{
  #NOP %1;
  #IF {"%1" == "$window[index][1]" || "%1" == "" }
  {
    #VAR {_WRL[rows]} {@fEval{@fScreen{{head}{rows}}-1}};
    #VAR {_WRL[offset]}  {&window[buffer][$window[index][1]][] - $_WRL[rows]};
    #IF {!$window[tail][%1] || $_WRL[offset] < 1}
    {
      #VAR {_WRL[offset]} {0};
    };
    #IF {%2 > 0 && %2 <= $_WRL[rows]}
    {
      #VAR {_WRL[i]} {@fEval{%2 + $_WRL[offset]}};
      #ECHO {{%-@fScreen{{full}{cols}}s<099>}{@fScreen{{full}{rows}}-%2}} {$window[buffer][$window[index][1]][$_WRL[i]]};
    };
  };
};

#ALIAS {.WindowRefresh}
{
  #NOP %1;
  #LOOP {1} {@fScreen{{head}{rows}}-1} {i}
  {
    .WindowRefreshLine {%1} {$i};
  };
  #IF {"%1" == "$window[index][1]"} #VAR {window[update][%1]} {0};
};
event_register {e_screen_resize} {h_window_refresh} {.WindowRefresh {$window[index][1]}};

#MACRO {\t} {.WindowSwap};
