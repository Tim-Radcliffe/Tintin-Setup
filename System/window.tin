#VAR {_Window} {};

#NOP {windowname} {windowline} {content};
#ALIAS {.WindowSetLine}
{
    #NOP %1;
    .WindowCreate {%1};
    #VAR {_Window[buffer][%1][%2]} {%3};
    .WindowUpdated {%1};
};

#ALIAS {.WindowSet}
{
    #NOP %1;
    .WindowCreate {%1};
    #VAR {_Window[buffer][%1]} {%2};
    .WindowUpdated {%1};
};

#ALIAS {.WindowAppend}
{
    #NOP %1;
    .WindowCreate {%1};
    #LIST {_Window[buffer][%1]} {add} {%2};
    #WHILE {&_Window[buffer][%1][] > @fScreen{{full}{rows}}}
    {
        #LIST {_Window[buffer][%1]} {delete} {1};
    };
    #VAR {_Window[tail][%1]} {1};
    .WindowUpdated {%1};
};

#ALIAS {.WindowUpdated}
{
    #NOP %1;
    #VAR {_Window[update][%1]} {1};
    #NOP .WindowNotify
};

#ALIAS {.WindowCreate}
{
    #NOP %1;
    #IF {&_Window[names][%1] == 0}
    {
        #IF {"%2" != ""}
        {
            #VAR {_Window[names][%1]} {%2};
        };
        #ELSE
        {
            #IF {@fScreen{{right}{cols}} > 5}
            {
                #VAR {_Window[names][%1]} {right};
            };
            #ELSE
            {
                #VAR {_Window[names][%1]} {head};
            };
        };
        #LIST {_Window[index][$_Window[names][%1]]} {ADD} {%1};
    };  
};

/*#ALIAS {.WindowNotify}
{
    #NOP %1;
    #VAR {_Window[notify]} {};
    #FOREACH {$_Window[names][1..-1]} {w}
    {
        #VAR {_Window[notify][$w][divbegin]} {\/};
        #VAR {_Window[notify][$w][divend]} {};
    };
    #FOREACH {*_Window[names][]} {n}
    {
	#VAR {w} {$_Window[names][$n]};
        #IF {"$n" == "$_Window[index][$w][1]"}
        {
            #VAR {_Window[notify][$w][text]} {${_Window[notify][$w][text]}<074>\/ $n \\<474>};
            #VAR {_Window[notify][$w][divbegin]} {};        
            #VAR {_Window[notify][$w][divend]} {\\};        
        };
        #ELSEIF {"$_Window[update][$n]" == "1"}
        {
            #VAR {_Window[notify][$w][text]} {${_Window[notify][$w][text]}${_Window[notify][$w][divbegin]}\*$n\*${_Window[notify][$w][divend]}};        
        };
        #ELSE
        {
            #VAR {_Window[notify][$w][text]} {${_Window[notify][$w][text]}${_Window[notify][$w][divbegin]} $n ${_Window[notify][$w][divend]}};
        };
    };
    #FORMAT {_Window[notify][line]} {%-@fScreen{{body}{cols}}s%-@fScreen{{right}{cols}}s} {$_Window[notify][head][text]} {$_Window[notify][right][text]};
    event_raise {e_notify} {window} {<474>$_Window[notify][line]} {2};    
}; */

#ALIAS {.WindowSwap}
{
    #NOP %1;
    #FOREACH {*_Window[index][]} {v}
    {
        #IF {"$_Window[index][$v][1]" == "" } {#RETURN;};
        #LIST {_Window[index][$v]} {add} {$_Window[index][$v][1]};
        #LIST {_Window[index][$v]} {delete} {1};
        .WindowRefresh {$_Window[index][$v][1]};
        event_raise {e_window_swap} {$v} {$_Window[index][$v][1]};
    };
};

#ALIAS {.WindowRefresh}
{
    #NOP %1;
    #FOREACH {*_Window[index][]} {s}
    {
        #IF {"%1" == "$_Window[index][$s][1]" || "%1" == ""}
        {
            #VAR {w} {$_Window[index][$s][1]};
            #DRAW TILE @fScreen{{$s}{y}} @fScreen{{$s}{x}} @fScreen{{$s}{y2}} @fScreen{{$s}{x2}} $_Window[buffer][$w][1..-1];
            #VAR {_Window[update][$w]} {0};
        };
    };
   #NOP .WindowNotify;
};
event_register {e_screen_resize} {h_window_refresh} {.WindowRefresh {$_Window[index][1]}};

#FUNCTION {fWindowActive}
{
    #FOREACH {*_Window[index][]} {v}
    {
        #IF {"$_Window[index][$v][1]" == "%0"} {#RETURN 1;};
    };    
    #RETURN 0;
};

#MACRO {\t} {.WindowSwap};