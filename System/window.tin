#VAR {window} {};

#NOP {windowname} {windowline} {content};
#ALIAS {.WindowSetLine}
{
  #NOP %1;
  .WindowCreate {%1};
  #VAR {window[buffer][%1][%2]} {%3};
  #VAR {window[update][%1]} {1};
};

#ALIAS {.WindowSet}
{
  #NOP %1;
  .WindowCreate {%1};
  #VAR {window[buffer][%1]} {%2};
  #VAR {window[update][%1]} {1};
};

#ALIAS {.WindowAppend}
{
  #NOP %1;
  .WindowCreate {%1};
  #LIST {window[buffer][%1]} {add} {%2};
#NOP    #VAR {_wa[head]} {@fEval{$session[HeadRows]-1}};
#NOP    #VAR {_wa[overflow]} {@fEval{&window[buffer][%1][] - $_wa[head]}};
#NOP    #IF {$_wa[overflow] > 0}
#NOP    {
#NOP      #LIST {window[buffer][%1]} {delete} {1} {$_wa[overflow]};
#NOP    };
#NOP    #UNVAR {_wa};
  #VAR {window[tail][%1]} {1};
  #VAR {window[update][%1]} {1};
};

#ALIAS {.WindowCreate}
{
  #NOP %1;
  #IF {"$window[names][%1]" != "1"}
  {
    #VAR {window[names][%1]} {1};
    #LIST {window[index]} {CREATE} {$window[names][]};
  };  
};

#ALIAS {.WindowSwap}
{
  #NOP %1;
  #IF {"$window[index][1]" == "" } {#RETURN;};
  #LIST {window[index]} {add} {$window[index][1]};
  #LIST {window[index]} {delete} {1};
  event_raise {e_notify} {window} {<274>Window Displayed: $window[index][1]} {2};
  .WindowRefresh {$window[index][1]};
};

#ALIAS {.WindowRefreshLine}
{
  #NOP %1;
  #IF {"%1" == "$window[index][1]" || "%1" == "" }
  {
    #VAR {_WRL[rows]} {@fEval{$session[HeadRows]-1}};
    #VAR {_WRL[offset]}  {&window[buffer][$window[index][1]][] - $_WRL[rows]};
    #IF {!$window[tail][%1] || $_WRL[offset] < 1}
    {
      #VAR {_WRL[offset]} {0};
    };
    #IF {%2 > 0 && %2 <= $_WRL[rows]}
    {
      #VAR {_WRL[i]} {@fEval{%2 + $_WRL[offset]}};
      #ECHO {{%-${session[ScreenCols]}s<099>}{$session[ScreenRows]-%2}} {$window[buffer][$window[index][1]][$_WRL[i]]};
    };
  };
};

#ALIAS {.WindowRefresh}
{
  #NOP %1;
  #LOOP {1} {$session[HeadRows]-1} {i}
  {
    .WindowRefreshLine {%1} {$i};
  };
};
event_register {e_screen_resize} {h_window_refresh} {.WindowRefresh};

#MACRO {\t} {.WindowSwap};
