#VAR {_WC} {};

#ALIAS {.WindowComboSplit}
{
    #NOP %1;
    #VAR {_WC[split][l]} {40};
    #VAR {_WC[split][r]} {@fEval{@fScreen{{full}{cols}}-${_WC[split][l]}}};
};

#ALIAS {.WindowComboRefresh}
{
    #NOP %1;
    #VAR {_WC[buffer]} {};
    #VAR {_WC[l]} {};
    #VAR {_WC[r]} {};

    #LIST {_WC[l]} {add} {$_WC[stats][%*]};
    #LIST {_WC[l]} {add} {$_WC[room][%*]};

    #LIST {_WC[r]} {add} {$_WC[map][%*]};

    #LOOP {1} {@fScreen{{head}{rows}}} {_WC[i]}
    {
        #VAR {_WC[buffer][$_WC[i]]} {@fLinePad{{$_WC[l][$_WC[i]]}{}{$_WC[split][l]}}$_WC[r][$_WC[i]]}
    };
    .WindowSet {combo} {$_WC[buffer]};
    .WindowRefresh {combo};
};

#ALIAS {.WindowComboStats}
{
    #NOP %1;
    .WindowComboSplit;
    #UNVAR {_WC[stats]};
    #FORALL {hp;sp;gp1;gp2}
    {
        #IF {"$my[&0][current]" == "0" && "$my[&0][max]" == "0"} {}
        {
            #IF {"$my[&0][name]" == ""} {#FORMAT {my[&0][name]} {%u} {&0};};
            #LIST {_WC[stats]} {add} {@fGauge{{$my[&0][name] : $my[&0][current]}{$my[&0][current]}{$my[&0][max]}{$my[&0][colour]}{$_WC[split][l]}}};
        };
    };
    #LIST {_WC[stats]} {add} {@fGauge{{Vs : $mip[FFF][K][data]}{$mip[FFF][L][data]}{100}{FAA}{$_WC[split][l]}}};
    .WindowComboRefresh;    
};
event_register {e_mip_FFF} {h_window_combo} {.WindowComboStats};
.WindowComboStats;

#ALIAS {.WindowComboRoom}
{
    #NOP %1;
    .WindowComboSplit;
    #UNVAR {_WC[room]};    
    #LIST {_WC[room]} {add} {@fTrunc{{Name  :$map[room][roomname]}{$_WC[split][l]}}};
    #LIST {_WC[room]} {add} {@fTrunc{{Exits :@fCommaList{$map[room][roomexits][]}}{$_WC[split][l]}}};
    #IF {"$map[room][roomnote]" != ""}
    {
        #LIST {_WC[room]} {add} {Notes :};
        #FORALL {$map[room][roomnote][]}
        {
            #LIST {_WC[room]} {add} {@fTrunc{{&0}{$_WC[split][l]}}};
        };
    };
};

#ALIAS {.WindowComboMap}
{
    #NOP %1;
    #IF {"$map[file]" == ""} #RETURN;
    .WindowComboSplit;
    #MAP MAP {${_WC[split][r]}x@fScreen{{head}{rows}}} {_WC[mapstring]} {v};
#NOP    #VAR {_WC[map]} {@fSplitString{$_WC[mapstring]}};
    #REPLACE {_WC[mapstring]} {\\} {\\\\};
    #FORMAT {_WC[map]} {%w} {{$_WC[mapstring]}};
#NOP    #MAP MAP {${_WC[split][r]}x@fScreen{{head}{rows}}} {/tmp/map3.txt};
#NOP    #SYSTEM {sed -i 's/\\/\\\\/g' /tmp/map3.txt};
#NOP    #SCRIPT {_WC[map]} {cat /tmp/map3.txt};
    .WindowComboRefresh;
};

event_register {e_map_refresh} {h_window_combo} {.WindowComboRoom;.WindowComboMap};
event_register {e_map_room_enter} {h_window_combo} {.WindowComboRoom;.WindowComboMap};
.WindowComboRoom;
.WindowComboMap;
