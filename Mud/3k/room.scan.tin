#CLASS {cRoomScan} {kill};
#CLASS {cRoomScan} {open};
#NOP **** Variables ****

#VAR {room} {};

#VAR {RoomScan[mob]} 
{
  {under the control of}{-1}
  {the Skeleton}{-1}
  {the Zombie}{-1}
  {the Valpur}{-1}
  {the Ghast}{-1}
  {the Manshi}{-1}
  {the Bolwyn}{-1}
  {the Greesh}{-1}
  {the Wight}{-1}
  {the Heucuva}{-1}
  {the Tholen}{-1}
  {the Bayen}{-1}
  {the Quarin}{-1}
  {the Mohrg}{-1}
  {the Grimare}{-1}
  {the Valithyn}{-1}
  {esquire}{-1}
  {'s}{-1}
};

#VAR {RoomScan[objects]} 
{ 
  {coins}{1}
  {phial}{1}
  {bag}{1}
  {fragment of}{1}
  {essence of}{1}
  {heart of}{1}
  {puppy}{1}
  {black stone}{-1}
  {grafting}{-1}
  {departing}{-1}
  {devouring}{-1}
  {slow digestion}{-1}
  {quick digestion}{-1}
  {hunger}{-1}
  {fractures}{-1}
  {nullification}{-1}
  {obliquity}{-1}
  {fumbling}{-1}
  {fear}{-1}
  {departing}{-1}
  {empty}{-1}
  {-\d.}{-1}  
};

#NOP **** Triggers ****
#NOP ** brief no map

#NOP ** brief/verbose with map
#NOP #ACTION {{^(\S.{68})[?:\@|\+|\^|v|E].*}}
{
	#REGEXP {%1} {{(.*) \(([a-z,]*)\).*$}}
	{
		#VAR {room} {};
		#VAR {room[name]} {&2};
		#NOP handler_parse_room_exits {&3};
        event_raise {e_room_header};
	};
} {2};

#NOP *********** Aliases **************;

#ALIAS {handler_room_contents}
{
  #NOP %1;
  #LIST {temp[tokenized]} {create} {%2};
  #VAR {temp[item][id]} {$temp[tokenized][2]};
  #VAR {temp[item][name]} {$temp[tokenized][3]};
  #REPLACE {temp[tokenized][4]} {/} {;};
  #REPLACE {temp[tokenized][4]} {#N} {$temp[tokenized][2]};
  #LIST {temp[item][actions]} {create} {$temp[tokenized][4]};
  #LIST {room[contents][$temp[tokenized][1]]} {add} {{$temp[item]}};
  event_raise e_room_${temp[tokenized][1]} {$temp[item][id]} {$temp[item][name]} {$temp[item][actions]};
};
event_register e_mip_HAA h_room_contents handler_room_contents;
event_register e_mip_HAB h_room_contents handler_room_contents;

#NOP #ALIAS {handler_room_mob}
#NOP {
#NOP     #LIST {room[contents][npc]} {add} {{{name}{%2}{id}{%2}}};
#NOP     event_raise e_room_npc {%2} {%2};
#NOP };
#NOP event_register {e_ansiflag_M} {room.scan.tin} {handler_room_mob}; 
#NOP 
#NOP #ALIAS {handler_room_item}
#NOP {
#NOP     #LIST {room[contents][item]} {add} {{{name}{%2}{id}{%2}}};
#NOP     event_raise e_room_item {%2} {%2};
#NOP };
#NOP event_register {e_ansiflag_A} {room.scan.tin} {handler_room_item}; 
#NOP event_register {e_ansiflag_W} {room.scan.tin} {handler_room_item}; 
#NOP event_register {e_ansiflag_O} {room.scan.tin} {handler_room_item}; 
#NOP 
#NOP #ALIAS {handler_room_player}
#NOP {
#NOP     #LIST {room[contents][player]} {add} {{{name}{%2}{id}{%2}}};
#NOP     event_raise e_room_player {%2};
#NOP };
#NOP event_register {e_ansiflag_P} {room.scan.tin} {handler_room_player}; 

#ALIAS {handler_room_end}
{
  #NOP %1;
  #IF {&room[contents][player][] <= 0}
  {
    #IF {$auto[kill][status] > 0 && &room[contents][npc][] > 0}
    {
      handler_autokill;
    };
    #IF {"&RoomScan[commandqueue][1]" != "0"}
    {
      #FORALL {$RoomScan[commandqueue][%*]} 
      {
        #SEND {&0};
      };
      #UNVAR {RoomScan[commandqueue]};
    };
    #IF {"$RoomScan[autokill_combat]" != "1"}
    {
      event_raise e_roomscan_finished;
    };
  }
  {
    event_raise e_roomscan_finished;
  };
};

#ALIAS {.HandlerRoomExits}
{
  #NOP %1;
  #IF {"$mip[last]" != "BAD"}
  {
    #VAR {room[exits]} {%2};
    #REPLACE {room[exits]} {,} {;};
    event_oneshot e_prompt h_room_enter {event_raise e_room_enter {#NOP %%1};};    
    event_oneshot e_prompt h_room_end handler_room_end;
  };
};
event_register {e_mip_DDD} {h_room_exits} {.HandlerRoomExits};

#ALIAS {.HandlerRoomName}
{
  #NOP %1;
  #VAR {room} {};      
  #REGEXP {%2} {{^(.*?)\s*\(([a-z\s,]*)\)\s*$}}
  {
    #VAR {room[name]} {&2};
#NOP     .HandlerRoomExits {RoomName} {&3};
  }
  {
    #VAR {room[name]} {%2};
#NOP    #UNVAR {room[exits]};
  };  
};
#NOP event_register {e_mip_BAD} {h_room_name} {.HandlerRoomName};
event_register {e_ansiflag_R} {h_room_name} {.HandlerRoomName};

#ALIAS {handler_room_pickup}
{
  #NOP %1;

  #FORALL {$RoomScan[objects][]}
  {
    #IF {"$RoomScan[break]" != "1"}
    {
      #REGEXP {%3} {\b&0\b}
      {
        #IF {"$RoomScan[objects][&0]" == "-1"}
        {
          #UNVAR {RoomScan[commandbuffer]};
          #VAR {RoomScan[break]} {1};
        }
        {
          #IF {"$RoomScan[objects][&0]" == "1"}
          {
            #VAR {RoomScan[commandbuffer]} {get %2};
          }
          {
            #VAR {RoomScan[commandbuffer]} {$RoomScan[objects][&0]};
          };
        };
      };
    };
  };
  
  #IF {"$RoomScan[commandbuffer]" != ""}
  {
    #LIST {RoomScan[commandqueue]} {add} {$RoomScan[commandbuffer]};
  };
  #ELSEIF {"$RoomScan[break]" != "1" && "$flags[autopickup]" == "2"}
  {
    #LIST {RoomScan[commandqueue]} {add} {get %2};
  };
  
  #UNVAR {RoomScan[break]};
  #UNVAR {RoomScan[commandbuffer]};
};

#ALIAS {autopickup}
{
  #IF {"%1" == "off" || "%1" == "stop"}
  {
    #VAR {flags[autopickup]} {0};
    #ECHO {Auto Pickup Off};
    #RETURN;
  };
  #IF {"%1" == "min"} {#VAR {flags[autopickup]} {0};};
  #IF {"%1" == "full"} {#VAR {flags[autopickup]} {1};};

  #SWITCH {"$flags[autopickup]"}
  {
    #CASE {"0"}
    {
      #VAR {flags[autopickup]} {1};
      #ECHO {Auto Pickup Minimal};
    }
    #CASE {"1"}
    {
      #VAR {flags[autopickup]} {2};
      #ECHO {Auto Pickup Full};
    };
    #CASE {"2"}
    {
      #VAR {flags[autopickup]} {1};
      #ECHO {Auto Pickup Minimal};
    };
  };

  #IF {$flags[autopickup] > 0 }
  {
    event_register e_room_item h_auto_pickup handler_room_pickup;
  };
  #ELSE
  {
    event_remove_handler h_auto_pickup;
  };
};

autopickup min;
#CLASS {cRoomScan} {close};
