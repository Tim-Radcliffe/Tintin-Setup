#NOP ************* Necromancer Guild *********;

#VAR {RoomScan[objects][a preserved, %+ corpse of]} {1};
#VAR {RoomScan[objects][crystal charm]}             {1};
#VAR {RoomScan[objects][platinum coin]}             {1};
#VAR {RoomScan[objects][small grey stone]}          {1};
#VAR {RoomScan[objects][dark leather strap]}        {1};
#VAR {RoomScan[objects][cloudy glass sphere]}       {1};
#VAR {RoomScan[objects][dark skull key]}            {1};

/* Teleport to Guild */
#ACTION {^Visions of the guild appear first in your mind, then reality...}
{
    #NOP %1;
    .mapLoad {Pinnacle/Fantasy/Necromancer Guild};
    #map goto {Receiving room};
};

/* Preserve when you kill an opponent */
#ALIAS {handler_auto_preserve}
{
    #NOP %1;
    #IF {$my[corpse][held][current] < $my[corpse][held][max]}
    {
        #SEND {!preserve get};
    };
    #ELSEIF {$auto[consecrate][status]}
    {
        #SEND {!consecrate};
    };
    #ELSE 
    {
        #SEND {!absorb ground};
        #SEND {!absorb};
        #SEND {!absorb inventory};
    };
    #SEND {!glance};
};
auto {add} {preserve} {} {e_killed_opponent} {}; 
auto {add} {consecrate} {} {} {}; 

/* Clean up corpses if you can't carry them */
#ACTION {^  You can't carry any more master, leaving it on the ground.}
{
    #SEND {!absorb ground};
    #IF {$my[hp][current] < $my[hp][max]}
    {
        #SEND {!absorb prime};
    };
    #ELSE
    {
        #SEND {!absorb};
    };
    #SEND {!absorb inventory};
} {3};

/* Call your familiar into combat automatically when you "kill" */
#ALIAS {handler_auto_call}
{
    #NOP %1;
    #SEND {!call};
};
auto {add} {call} {} {e_combat} {};

/* Corrode your opponent automatically when you "kill" */
#ALIAS {handler_auto_corrode}
{
    #NOP %1;
    #IF {$my[necromancer][power][corrode] > 0 &&
         $my[gp1][current] > 50 &&
         $my[sp][current] > 50}
    {
        #SEND {!corrode};
    };
};
auto {add} {corrode} {} {e_combat} {};

/* Contribute half of coins picked up to the guild, between 10 and 1000 */
#ALIAS {handler_auto_contribute}
{
    #NOP %1;
    #REGEXP {%2} {coins (%d)}
    {
        #IF {@fEval{&1/2} > 9}
        {
            #SEND {!contribute @fMin{@fEval{&1/2};1000}};
        };
    };
    #IF {$my[necromancer][worth] < 125}
    {
        #SEND {!contribute};
    };
};
auto {add} {contribute} {} {e_pickup;e_combat_round} {};

/* SPFuse when below 25% SP (it refills by itself when SP is full) */
#ALIAS {handler_auto_spfuse}
{
    #NOP %1;
    #VAR {_necSP[Diff]} {@fEval{($my[sp][max]+0.00-($my[sp][max]-$my[sp][current]))/$my[sp][max]}};
    #IF {$_necSP[Diff] < 0.25 && $my[necromancer][spfuse][current] > 0} 
    {
        #SEND {!spfuse $my[necromancer][spfuse][current]};
    };
};
auto {add} {spfuse} {} {e_combat_round} {};

/* Automatically recast protection (if it's memorised and there's enough NP */
#ALIAS {handler_auto_protection}
{
    #NOP %1;
    #IF {"$my[necromancer][protection]" != "ON" && 
         $my[necromancer][power][protection] > 0 &&
         $my[gp1][current] > 55}
    {
        #SEND {!protection};
    };
};
auto {add} {protection} {} {e_combat_round} {};

/* Gag Necro message spam */
#ALIAS {necro_gag_on}
{
    #GAG {^-You activate your atrance-};
    #GAG {^-Your atrance is complete-};
    #GAG {^C:* You blink into unconsciousness *};
    #GAG {^* You recover your senses *};
    #GAG {^>>Atrancing};
    #GAG {^$my[necromancer][familiar] tells you:};
    #GAG {^  You'll have to wait a moment master.};
    #GAG {^  You will now absorb corpses from %+ first master.};
};

/* Ungag Necro message spam */
#ALIAS {necro_gag_off}
{
    #UNGAG {^-You activate your atrance-};
    #UNGAG {^-Your atrance is complete-};
    #UNGAG {^C:* You blink into unconsciousness *};
    #UNGAG {^* You recover your senses *};
    #UNGAG {^>>Atrancing};
    #UNGAG {^$my[necromancer][familiar] tells you:};
    #UNGAG {^  You'll have to wait a moment master.};
    #UNGAG {^  You will now absorb corpses from %+ first master.};    
}
auto {add} {necrogag} {necro_gag_on} {} {necro_gag_off};

/* Automatically fire glamors */
#ALIAS {handler_auto_glamor}
{
    #NOP %1;
    #IF {(@fEval{$my[necromancer][glamors][max]-$my[necromancer][glamors][current]} < 
          @fEval{$my[gp2][current]/(100/$my[necromancer][glamors][max])}) &&
          "$my[necromancer][glamors][active]" == ""}
    {
        #SEND {!illume};
    };
};
auto {add} {glamor} {} {e_combat_round} {};
