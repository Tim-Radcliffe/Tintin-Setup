#NOP ************* Necromancer Guild *********;

/* Teleport to Guild */
#ACTION {^Visions of the guild appear first in your mind, then reality...}
{
    #NOP %1;
    .mapLoad {Pinnacle/Fantasy/Necromancer Guild};
    #map goto {Receiving room};
};

/* Preserve when you kill an opponent */
#ALIAS {handler_auto_preserve}
{
    #NOP %1;
    #SEND {!preserve remains};
    #SEND {!get preservation};
    #SEND {!glance};
};
auto {add} {preserve} {} {e_killed_opponent} {}; 

/* Call your familiar into combat automatically when you "kill" */
#ALIAS {handler_auto_call}
{
    #NOP %1;
    #REGEX {%2} {^{k|kill}%s%*}
    {
        event_oneshot {e_prompt} {necromancer} {#NOP %%1;#SEND {!call};};
    };
};
auto {add} {call} {} {e_command} {};

/* Corrode your opponent automatically when you "kill" */
#ALIAS {handler_auto_corrode}
{
    #NOP %1;
    #IF {$my[necromancer][power][corrode] > 0 &&
         $my[gp1][current] > 50 &&
         $my[sp][current] > 50}
    {
        #REGEX {%2} {^{k|kill} %*}
        {
            #SEND {!corrode &2};
        };
    };
};
auto {add} {corrode} {} {e_command} {};

/* Contribute half of coins picked up to the guild, between 10 and 1000 */
#ALIAS {handler_auto_contribute}
{
    #NOP %1;
    #REGEXP {%2} {coins (%d)}
    {
        #IF {@fEval{&1/2} > 9}
        {
            #SEND {!contribute @fMin{@fEval{&1/2};1000}};
        };
    };
};
auto {add} {contribute} {} {e_pickup} {};

/* SPFuse when below 25% SP (it refills by itself when SP is full) */
#ALIAS {handler_auto_spfuse}
{
    #NOP %1;
    #VAR {_necSP[Diff]} {@fEval{($my[sp][max]+0.00-($my[sp][max]-$my[sp][current]))/$my[sp][max]}};
    #IF {$_necSP[Diff] < 0.25 && $my[necromancer][spfuse][current] > 0} 
    {
        #SEND {!spfuse $my[necromancer][spfuse][current]};
    };
};
auto {add} {spfuse} {} {e_combat_round} {};

/* Automatically recast protection (if it's memorised and there's enough NP */
#ALIAS {handler_auto_protection}
{
    #NOP %1;
    #IF {"$my[necromancer][protection]" != "ON" && 
         $my[necromancer][power][protection] > 0 &&
         $my[gp1][current] > 55}
    {
        #SEND {!protection};
    };
};
auto {add} {protection} {} {e_combat_round} {};

/* Gag Necro message spam */
#ALIAS {necro_gag_on}
{
    #GAG {^-You activate your atrance-};
    #GAG {^-Your atrance is complete-};
    #GAG {^C:* You blink into unconsciousness *};
    #GAG {^* You recover your senses *};
    #GAG {^>>Atrancing};
    #GAG {^$my[necromancer][familiar] tells you:};
    #GAG {^  You'll have to wait a moment master.};
};

/* Ungag Necro message spam */
#ALIAS {necro_gag_off}
{
    #UNGAG {^-You activate your atrance-};
    #UNGAG {^-Your atrance is complete-};
    #UNGAG {^C:* You blink into unconsciousness *};
    #UNGAG {^* You recover your senses *};
    #UNGAG {^>>Atrancing};
    #UNGAG {^$my[necromancer][familiar] tells you:};
    #UNGAG {^  You'll have to wait a moment master.};
}
auto {add} {necrogag} {necro_gag_on} {} {necro_gag_off};
