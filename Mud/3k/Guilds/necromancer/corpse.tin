#NOP Necromancer corpse handling scripts.

/* Central command for storing/retrieving/managing corpses */
#ALIAS {.corpse}
{
    #NOP %1;
    #IF {"%1" == ""}
    {
        #ECHO {.corpse store or .corpse retrieve};
    };
    #ELSEIF {"%1" == "store"}
    {
        #IF {$my[corpse][smuggle][current] < $my[corpse][smuggle][max]}
        {
            #SEND {!smuggle corpse};
            #VAR {my[corpse][smuggle][current]} {@fEval{$my[corpse][smuggle][current]+1}};
            #VAR {my[corpse][held][current]} {@fEval{$my[corpse][held][current]-1}};
        };
        #ELSEIF {$my[corpse][coffin][current] < $my[corpse][coffin][max]}
        {
            #SEND {!wrap corpse};
            #VAR {my[corpse][coffin][current]} {@fEval{$my[corpse][coffin][current]+1}};
            #VAR {my[corpse][held][current]} {@fEval{$my[corpse][held][current]-1}};
        };
        #ELSEIF {$my[corpse][minion][current] < $my[corpse][minion][max]}
        {
            #SEND {!give corpse to undead follower};
            #VAR {my[corpse][minion][current]} {@fEval{$my[corpse][minion][current]+1}};
            #VAR {my[corpse][held][current]} {@fEval{$my[corpse][held][current]-1}};
        };
    };
    #ELSEIF {"%1" == "retrieve"}
    {
        #IF {$my[corpse][smuggle][current] > 0}
        {
            #SEND {!smuggle remove corpse};
            #VAR {my[corpse][smuggle][current]} {@fEval{$my[corpse][smuggle][current]-1}};
            #VAR {my[corpse][held][current]} {@fEval{$my[corpse][held][current]+1}};
        };
        #ELSEIF {$my[corpse][coffin][current] > 0}
        {
            #SEND {!unwrap corpse};
            #SEND {!get preservation};
            #VAR {my[corpse][coffin][current]} {@fEval{$my[corpse][coffin][current]-1}};
            #VAR {my[corpse][held][current]} {@fEval{$my[corpse][held][current]+1}};
        };
        #ELSEIF {$my[corpse][minion][current] > 0}
        {
            #SEND {!=give preservation};
            #VAR {my[corpse][minion][current]} {@fEval{$my[corpse][minion][current]-1}};
            #VAR {my[corpse][held][current]} {@fEval{$my[corpse][held][current]+1}};
        };
    };
    #ELSEIF {"%1" == "sort"}
    {
        .file load {$path[guild]/scripts/CorpseSort.tin};
    };
};

/* Automatically Stores/Retrieves corpses as required */
#ALIAS {handler_auto_corpse}
{
    #NOP %1;
    #IF {$my[corpse][held][current] < $my[corpse][held][max]}
    {
        #LOOP 1 {@fEval{$my[corpse][held][max] - $my[corpse][held][current]}} {}
        {
            .corpse retrieve;
        };
    };
    #ELSEIF {$my[corpse][held][current] > $my[corpse][held][max]}
    {
        #LOOP 1 {@fEval{$my[corpse][held][current] - $my[corpse][held][max]}} {}
        {
            .corpse store;
        };
    };
};
auto {add} {corpse} {} {e_combat_round} {};

/* Returns an integer value for corpse size */
#FUNCTION {fCorpseSize}
{
    #REGEXP {%0} {{([\(\[])([\|\*\-]{5})[\)\]]}}
    {
        #IF {"&2" == "["}
        {
            #VAR {result} {11};
        };
        #ELSE
        {
            #PARSE {&3} {result[c]}
            {
                #MATH {result[i]} {$result[i] + 1};
                #IF {"$result[c]" == "|"} 
                {
                    #VAR {result} {$result[i]};
                    #BREAK;
                };
                #IF {"$result[c]" == "*"} 
                {
                    #MATH {result} {11 - $result[i]};
                    #BREAK;
                };
            };
        };
    };
};
