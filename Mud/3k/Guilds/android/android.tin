#ACTION {^{>\s|}HP:\[(\d+)\/(\d+)\]\s+CHARGE:\[(\d+)Ah\/(\d+)Ah\]\s+G2N:\[([0-9.]+)\%\]\s+P:(.)\s+E:(.*)}
{
  #VAR {my[hp][current]} {%1};
  #VAR {my[hp][max]} {%2};
  #VAR {my[gp1][current]} {%3};
  #VAR {my[gp1][max]} {%4};
  #VAR {my[android][g2n]} {%5};
  #VAR {my[android][ep]} {%6};
};

#ACTION {^{>\s|}You vanquish your foe with a final %*.}
{
  event_raise e_killed_opponent {%*};
};

#ALIAS {gs2}
{
  #NOP %1;
  #ACTION {{^\s{3}(\S+)\s+\((\d+)\/(\d+)\)}}
  {
    #FORMAT {_gs2[name]} {%l} {%%2};
    #VAR {my[android][$_gs2[name]]} {%%3};
  };
  event_oneshot {e_prompt} {h_gs2} {#UNACTION {{^\s{3}(\S+)\s+\((\d+)\/(\d+)\)}}};
  #SEND {!gs2};
};

#ALIAS {handler_corpse}
{
  #NOP %1;
  #SEND {!get coins from corpse};
  #SEND {!wrap corpse};
  #SEND {!flush corpse};
  #SEND {!glance};
};
event_register e_killed_opponent h_corpse_handler handler_corpse;

#ALIAS {handler_auto_nanosurge}
{
  #NOP %1;
  #IF {$my[hp][current] < $my[hp][max] && 
       $my[gp1][current] > @fEval{$my[android][nanobots] * 5} && 
       $my[android][nanobots] > 0}
  {
    #SEND {!nanosurge};
  };

  #NOP /* Send second nanosurge if HP is less than 80% */;
  #IF {$my[hp][current] < $my[hp][max] * 0.8 && 
       $my[gp1][current] > @fEval{$my[android][nanobots] * 10} && 
       $my[android][nanobots] > 5}
  {
    #SEND {!nanosurge};
  };
  
};
auto {add} {nanosurge} {} {e_combat_round} {};
auto {nanosurge} {on};

#NOP /* // Error: You do not have enough charge to sustain engaged plating! */
