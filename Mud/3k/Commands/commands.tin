#NOP /******************************************************************\;
#NOP | File: general.commands.tin                                       |;
#NOP | Desc: Handle non-guild commands that get sent to the mud         |;
#NOP | Date: 27th August 2013                                           |;
#NOP | Balthus                                                          |;
#NOP \******************************************************************/;

#CLASS {cCommand} {kill};
#CLASS {cCommand} {open};

#ALIAS {i}
{
  event_raise e_inv_start;
  #CLASS {cCommand_i} {open};
  
  #ACTION {{^\s?(\d{1,2})\s+(?:\*|)(.*)\.$}}
  {
    #FORMAT {temp} {%l} {%%3};
    #LOOP 1 %%2 loop
    {
      event_raise e_inv_item {$temp};
    };
  } {1};
  
  #CLASS {cCommand_i} {close};

  event_oneshot {e_prompt} {h_command_i} {#CLASS {cCommand_i} {kill}};
  
  #SEND {i %0};
};

#ALIAS {rating}
{
  #VAR {room[location]} {};
  
  #CLASS {cCommand_rating} {open};
  
  #ACTION {{^AREA NAME.*?:\s(.*)\s.*\[(.*)\]$}}
  {
    #IF {"$room[location][name]" != "%%2" || "$room[location][wiz]" != "%%3"}
    {
      #VAR {room[location][name]} {%%2};
      #VAR {room[location][wiz]} {%%3};
    };
    event_oneshot {e_prompt} {h_command_rating} {.HandlerCommandRating}; 
  } {1};

  #ACTION {^AREA RATING -> %* [%w]}
  {
    #IF {"$room[location][name]" != "%%2"}
    {
      #VAR {room[location][name]} {%%2};
      #VAR {room[location][wiz]} {};
    };
    event_oneshot {e_prompt} {h_command_rating} {.HandlerCommandRating}; 
  } {1};

  #ACTION {^This area has no rating.  Please use the idea command to request one.}
  {
    #IF {"$room[location][name]" != ""}
    {
      #VAR {room[location][name]} {};
    };
    where;
    event_oneshot {e_prompt} {h_command_rating} {.HandlerCommandRating}; 
  } {1};

  #ALIAS {.HandlerCommandRating}
  {
    #NOP %%1;
    event_raise {e_command_rating};
    #CLASS {cCommand_rating} {kill};
  }
 
  #CLASS {cCommand_rating} {close};
  
  #SEND {!rating};
};

#ALIAS {where}
{
  #CLASS {cCommand_where} {open};
  #ACTION {^You're in %w'%* realm}
  {
    #VAR {room[location][wiz]} {%%1};
    #IF {"$room[location][name]" == ""}
    {
      #VAR {room[location][name]} {%%1};
    };
    event_oneshot {e_prompt} {h_command_where} {.HandlerCommandWhere}; 
  } {1};

  #ACTION {^You're in one of our main Realms.}
  {
    #VAR {room[location][name]} {Mud};
    event_oneshot {e_prompt} {h_command_where} {.HandlerCommandWhere}; 
  } {1};

  #ALIAS {.HandlerCommandWhere}
  {
    #NOP %%1;
    event_raise {e_command_where};
    #CLASS {cCommand_where} {kill};
  }
  #CLASS {cCommand_where} {close};
  
  #SEND {!where};
};

#ALIAS {x}
{
  #NOP %1;
  #SEND {!exa %0};
  #SEND {!search %0};
};

#CLASS {cCommand} {close};
